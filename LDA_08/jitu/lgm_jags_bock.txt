model{
  
  for (i in 1:N){
    # observed variables
    muy[i,1] <- eta[i,1];
    muy[i,2] <- eta[i,1]+1*eta[i,2];
    muy[i,3] <- eta[i,1]+2*eta[i,2];
    muy[i,4] <- eta[i,1]+3*eta[i,2];
    muy[i,5] <- eta[i,1]+4*eta[i,2];
    muy[i,6] <- eta[i,1]+5*eta[i,2];
    
    #latent variables
    # this is just a preparation for later
    mueta[i,1] <- beta0[1]
    mueta[i,2] <- beta0[2]
  }
  
  
  
  #///////////////////////////////////
  #// distribution of y
  #///////////////////////////////////
  for(i in 1:N){
    for(k in 1:6) {
      y[i,k] ~ dnorm(muy[i,k],psiy[k]);
    }
  
    #///////////////////////////////////
    #// distribution of factors
    #// note: eta1 and eta2 are correlated
    #///////////////////////////////////
    eta[i,1:2] ~ dmnorm(mueta[i,1:2],psieta[1:2,1:2]);
  }

  #///////////////////////////////////
  #// priors
  #///////////////////////////////////
  # regression coefficients
  for(k in 1:2){
    beta0[k] ~ dnorm(0,0.01);
  }
  
  #precisions
  for(k in 1:6){
    psiy[k] ~ dgamma(1,1);
  }
  
  psieta[1:2,1:2] ~ dwish(psi0[1:2,1:2],2);

  #///////////////////////////////////
  # transformations
  #///////////////////////////////////
  for(k in 1:6){sigmay[k] <- 1/psiy[k]}
  sigmaeta[1:2,1:2] <- inverse(psieta[1:2,1:2])
  
  
}